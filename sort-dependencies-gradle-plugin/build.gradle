plugins {
  id 'com.squareup.plugin'
  alias(libs.plugins.dokka)
  alias(libs.plugins.mavenPublish)
}

gradlePlugin {
  plugins {
    sortDependenciesPlugin {
      id = 'com.squareup.sort-dependencies'
      implementationClass = 'com.squareup.sort.SortDependenciesPlugin'
    }
  }
}

dependencies {
  testImplementation libs.spock
}

tasks.named('test') {
  // We need to publish the app to a local repo for use by the tests, and then clean that up afterwards.
  dependsOn ':app:publishAppPublicationToTestsRepository'
  finalizedBy ':app:cleanTestRepo'
  inputs.property('version', version)

  systemProperty 'rootDir', rootDir.absolutePath
}

tasks.named('processResources') {
  inputs.property('version', version)

  from 'com-squareup-sort-version.txt'
  expand(version: version)
}

tasks.withType(GenerateModuleMetadata).configureEach {
  dependsOn(tasks.named { it == "dokkaJavadocJar" })
}
